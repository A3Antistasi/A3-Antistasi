/*
    Author: [HÃ¥kon]
    [Description]
        Rebuilds category list from local garage pool data

    Arguments:
    0. <Control>    List control
    1. <Int>        Index of list to rebuild (same as control IDD last number)

    Return Value:
    <nil>

    Scope: Any
    Environment: Any
    Public: [No]
    Dependencies: [_ctrl, _index] call HR_GRG_fnc_reloadCategory;

    Example:

    License: APL-ND
*/
#include "defines.inc"
FIX_LINE_NUMBERS()
params ["_ctrl", "_catIndex"];
if (isNil "HR_GRG_Vehicles") exitWith {Trace("Reloading category canceled no garage data");};
Trace("Reloading category");

lbClear _ctrl;
private _selected = -1;
private _HR_GRG_SelectedVehicles = [-1,-1,""];
{
    _y params ["_displayName", "_class", "_lockedUID", "_checkedOut", "", ["_lockName", ""]];
    private _index = _ctrl lbAdd _displayName;
    _ctrl lbSetData [_index, str _x];
    _ctrl lbSetValue [_index, _x];
    _ctrl lbSetPicture [_index, cfgIcon(_class)];
    _ctrl lbSetPictureColor [_index, [1, 1, 1, 1]];
    _ctrl lbSetPictureColorSelected [_index, [0.85, 0.85, 0.55, 1]];

    private _tooltipText = "";
    if !( _checkedOut isEqualTo "" ) then {
        private _color = [1,0.1,0.1,1];
        if ( (HR_GRG_SelectedVehicles#1) isEqualTo _x ) then {
            _selected = _index;
            _color = [1,1,1,1];
        };
        _ctrl lbSetPictureRight [_index, CheckOutIcon];
        _ctrl lbSetPictureRightColor [_index, _color];
        _tooltipText = name (_checkedOut call BIS_fnc_getUnitByUID) +" "+( localize "STR_HR_GRG_Feedback_checkedOutToolTip" )+" ";
    };

    if !( _lockedUID isEqualTo "" ) then {
        private _color = if ( _lockedUID isEqualTo HR_GRG_PlayerUID ) then { [1,1,1,1] } else { [1,0.1,0.1,1] }; //white, red
        _ctrl lbSetPictureRight [_index, LockIcon];
        _ctrl lbSetPictureRightColor [_index, _color];
        _tooltipText = _tooltipText + ( localize "STR_HR_GRG_Feedback_LockedToolTip" )+" "+ _lockName;
    };
    _ctrl lbSetTooltip [_index, _tooltipText];
    _ctrl lbSetPictureRightColorSelected [_index, [0.85,0.85,0.55,1]];

} forEach (HR_GRG_Vehicles#_catIndex);
_ctrl lbSetCurSel _selected;
